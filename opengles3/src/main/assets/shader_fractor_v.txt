#version 300 es
layout (location = 0) in vec2 vPosition;

out vec3 aColor;

vec3 generateColor(int k) {
    int r, g, b;

    if (k < 16) {
        g = 0;
        b = 16 * k - 1;
        r = b;
    } else if (k < 32) {
        g = 16 * (k - 16);
        b = 16 * (32 - k) - 1;
        r = g;
    } else if (k < 64) {
        g = 8 * (64 - k) - 1;
        r = g;
        b = 0;
    } else { // range is 64 - 127
        r = 0;
        g = 0;
        b = 0;
    }
    return vec3(float(r) / 255.0, float(g) / 255.0, float(b) / 255.0);
}

vec2 mul(vec2 z, vec2 c){
    return vec2(z.x * c.x - z.y * c.y, z.y * c.x + z.x * c.y);
}

void main() {
    gl_Position  = vec4(vPosition / 200.0, 0.0, 1.0);
    gl_PointSize = 5.0;

    vec2 c = vec2(0.285, 0.01);
    vec2 z = vPosition / 200.0;

    int k = 0;
    for (; k < 64; k++) {
        if (z.x * z.x + z.y * z.y > 4.0) break;
        z = mul(z,z) + c;
    }

    aColor = generateColor(k);
}
